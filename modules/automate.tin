#ACTION {%1 to the {east|down|north|south|west|up} seems to be locked.$}
{
	#if {&{auto.pick}}
	{
		#regexp {%1} {{.*} {.*}} {#variable {auto.pick} {&2 %2}};
		#else
		{
			#variable {auto.pick} {%1 %2}
		};
		pick ${auto.pick}
	}
}
{9}

#ACTION {%1 {charges into you head first|punch contacts with your chest, causing immense pain|bashes you square on, throwing you flat on your back|flings you head over heels}{!|\.|\)|\]}$}
{
	#2 stand
}
{9}

#ACTION {^%w {pokes|points at} %S$}
{
	#if {"%1" == "$leader"}
	{
		#if {&{auto.poke}}
		{
			#regexp {${auto.poke}} {{.*?};{.*}} {&1 %3;&2};
			#else
			{
				${auto.poke} %3
			}
		}
	}
}
{9}

#ACTION {^The effects of battle cry are gone.$}
{
	#if {"${auto.warcry}" == "true"}
	{
		warcry
	}
}
{9}

#ACTION {^The pink glow around your body fades.$}
{
	#if {&{auto.ffire}}
	{
		#if {$MSDP_OPPONENT_HEALTH == 0}
		{
			${auto.ffire}
		};
		#else
		{
			#list {onDeath} {add} {${auto.ffire}}
		}
	}
}
{9}

#ACTION {^The white aura around your body fades.$}
{
	#if {&{auto.sanc}}
	{
		#if {$MSDP_OPPONENT_HEALTH == 0}
		{
			${auto.sanc}
		};
		#else
		{
			#list {onDeath} {add} {${auto.sanc}}
		}
	}
}
{9}

#ACTION {^You are {hungry|thirsty}.$}
{
	#if {"%1" == "hungry"}
	{
		#if {&{auto.eat}}
		{
			${auto.eat}
		}
	};
	#else
	{
		#if {&{auto.drink}}
		{
			${auto.drink}
		}
	}
}
{9}

#ACTION {^You feel less righteous.$}
{
	#if {&{auto.bless}}
	{
		#if {$MSDP_OPPONENT_HEALTH == 0}
		{
			${auto.bless}
		};
		#else
		{
			#list {onDeath} {add} {${auto.bless}}
		}
	}
}
{9}

#ACTION {^You just can't concentrate enough.$}
{
	#line {gag};
	meditate
}
{9}

#ACTION {^You must be sitting to concentrate.$}
{
	#line {gag};
	sit;
	meditate
}
{9}

#ACTION {^You start searching for tracks but then realize your target is right here!$}
{
	#if {&{auto.track}}
	{
		SYNTH_PLAY notificationSound;
		#unvariable {auto.track}
	}
}
{9}

#ACTION {^You try to work up a battle song but just can't muster up the enthusiasm.$}
{
	#if {"${auto.warcry}" == "true"}
	{
		warcry
	}
}
{9}

#ACTION {^You wake and sit up.$}
{
	stand
}
{9}

#ACTION {^You {stop focusing|attempt to focus but become distracted by butterflies}.$}
{
	#if {&{auto.focus}}
	{
		focus ${auto.focus}
	}
}
{9}

#ACTION {^You're {coated in ACID|on FIRE}, {it eats away at|the flames burn} your skin!}
{
	#if {"${auto.tumble}" == "true"}
	{
		tumble
	}
}
{9}

#ACTION {^Your body returns to its natural form{\.|\.\s*}$}
{
	#if {&{auto.shape}}
	{
		#if {$MSDP_OPPONENT_HEALTH == 0}
		{
			shape ${auto.shape}
		};
		#else
		{
			#list {onDeath} {add} {shape ${auto.shape}}
		}
	}
}
{9}

#ACTION {^Your eyes adjust back to normal.$}
{
	#if {&{auto.eyes}}
	{
		#if {$MSDP_OPPONENT_HEALTH == 0}
		{
			trueseeing
		};
		#else
		{
			#list {onDeath} {add} {trueseeing}
		}
	}
}
{9}

#ACTION {^Your metabolism slows.$}
{
	#if {&{auto.vigor}}
	{
		#if {"$MSDP_OPPONENT_NAME" != ""}
		{
			#list {onDeath} {add} {${auto.vigor}}
		};
		#else
		{
			${auto.vigor}
		}
	}
}
{9}

#ACTION {^Your skin becomes less tolerant to damage.$}
{
	#if {&{auto.barkskin}}
	{
		#if {"$MSDP_OPPONENT_NAME" == ""}
		{
			${auto.barkskin}
		};
		#else
		{
			#list {onDeath} {add} {${auto.barkskin}}
		}
	}
}
{9}

#ACTION {^Your warpaint wears off.$}
{
	#if {&{auto.warpaint}}
	{
		#if {$MSDP_OPPONENT_HEALTH == 0}
		{
			warpaint ${auto.warpaint}
		};
		#else
		{
			#list {onDeath} {add} {warpaint ${auto.warpaint}}
		}
	}
}
{9}

#ACTION {^[* You found your target! *]$}
{
	#if {&{auto.track}}
	{
		SYNTH_PLAY notificationSound;
		#unvariable {auto.track}
	}
}
{9}

#ACTION {^[* You see your target's tracks leading %1. *]$}
{
	#if {&{leader} || &{auto.track}}
	{
		%1
	}
}
{9}

#ACTION {^[* You {fail to find|lose} the tracks. *]$}
{
	#unvariable {auto.track}
}
{9}

#ALIAS {^autoheal%s {me|self} %3}
{
	#variable {autoHealSelf} {@strip_braces{%3}};
	#echo {You will automatically heal yourself using: $autoHealSelf\.}
}
{8}

#ALIAS {^noautoheal%s {me|self}$}
{
	#unvariable {autoHealSelf};
	#echo {You will no longer try to heal yourself automatically.}
}
{8}

#ALIAS {^/$}
{
	#if {$MSDP_LEVEL <= 20}
	{
		recall
	};
	#else
	{
		recite recall;
		get recall $character.container
	}
}
{9}

#ALIAS {^p %s%S$}
{
	emote ${automate.action} %2;
	#if {"${automate.action}" == "pokes"}
	{
		#variable {automate.action} {points at}
	};
	#else
	{
		#variable {automate.action} {pokes}
	}
}
{9}

#ALIAS {^{noauto|auto}{barkskin.*|bless.*|drink.*|eat.*|eyes|ffire.*|focus.*|pick|poke.*|recall.*|sanc.*|shape \w*|tumble|vigor.*|warcry|warpaint.*}$}
{
	#if {"%0" == "noauto%*"}
	{
		#echo {auto%2 disabled.};
		#unvariable {auto.%2}
	};
	#else
	{
		#regexp {%0} {auto{\w*} {.*}} {#echo {auto&1 set to &2.};#variable {auto.&1} {@strip_braces{&2}}};
		#else
		{
			#variable {auto.%2} {true};
			#echo {auto%2 enabled.}
		}
	}
}
{9}

#ALIAS {^{notrack|track .*}$}
{
	#if {"%0" == "notrack"}
	{
		#unvariable {auto.track};
		hunt
	};
	#else
	{
		#regexp {%0} {track {.*}} {#variable {auto.track} {&1};hunt &1}
	}
}
{9}

#VARIABLE         {automate}  {{action}{pokes}}
