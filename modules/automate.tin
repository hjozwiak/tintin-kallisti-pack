#ACTION {^You are {hungry|thirsty}.$}
{
	#if {"%1" == "hungry"}
	{
		#if {&{auto.eat}}
		{
			#if {&{character.container}}
			{
				get ${auto.eat} ${character.container};
				eat ${auto.eat}
			};
			#else
			{
				#echo {In order to use autoeat, you need to set your container. To do so, type container name, as in container bag.}
			}
		}
	}
}
{5}

#ACTION {^The effects of battle cry are gone.$}
{
	#if {"${auto.warcry}" == "true"}
	{
		warcry
	}
}
{9}

#ACTION {^The white aura around your body fades.$}
{
	#if {"${auto.sanc}" == "{pot|potion|purple|purps|purp}"}
	{
		get sanctuary ${character.container};
		quaff sanctuary
	};
	#else
	{
		#nop {Sanc and unholy aura can't be cast in combat, so this is a bit of a safety measure.};
		#if {$MSDP_OPPOMENT_HEALTH == 0}
		{
			${auto.sanc}
		};
		#else
		{
			#list {onDeath} {add} {${auto.sanc}}
		}
	}
}
{9}

#ACTION {^You just can't concentrate enough.$}
{
	#line {gag};
	meditate
}
{9}

#ACTION {^You must be sitting to concentrate.$}
{
	#line {gag};
	sit;
	meditate
}
{9}

#ACTION {^You start searching for tracks but then realize your target is right here!$}
{
	#if {&{auto.track}}
	{
		SYNTH_PLAY notificationSound;
		#unvariable {auto.track}
	}
}
{9}

#ACTION {^You try to work up a battle song but just can't muster up the enthusiasm.$}
{
	#if {"${auto.warcry}" == "true"}
	{
		warcry
	}
}
{9}

#ACTION {^You wake and sit up.$}
{
	stand
}
{9}

#ACTION {^You're {coated in ACID|on FIRE}, {it eats away at|the flames burn} your skin!}
{
	#if {"${auto.tumble}" == "true"}
	{
		tumble
	}
}
{9}

#ACTION {^Your metabolism slows.$}
{
	#if {$MSDP_OPPONENT_HEALTH > 0 && "${auto.vigor}" == "vig%*"}
	{
		#list {onDeath} {add} {${auto.vigor}}
	};
	#else
	{
		${auto.vigor}
	}
}
{9}

#ACTION {^[* You fail to find the tracks. *]$}
{
	#unvariable {auto.track}
}
{9}

#ACTION {^[* You found your target! *]$}
{
	#if {&{auto.track}}
	{
		SYNTH_PLAY notificationSound;
		#unvariable {auto.track}
	}
}
{9}

#ACTION {^[* You see your target's tracks leading %1. *]$}
{
	#if {&{leader} || &{auto.track}}
	{
		%1
	}
}
{9}

#ALIAS {^/$}
{
	#if {$MSDP_LEVEL <= 20}
	{
		recall
	};
	#else
	{
		recite recall;
		get recall $character.container
	}
}
{9}

#ALIAS {^{noauto|auto}{drink \w*|eat \w*|recall .*|sanc .*|tumble|vigor .*|warcry}$}
{
	#if {"%0" == "noauto%*"}
	{
		#echo {auto%2 disabled.};
		#unvariable {auto.%2}
	};
	#else
	{
		#regexp {%0} {auto{\w*} {.*}} {#echo {auto&1 set to &2.};#variable {auto.&1} {&2}};
		#else
		{
			#variable {auto.%2} {true};
			#echo {auto%2 enabled.}
		}
	}
}
{9}

#ALIAS {^{notrack|track \w*}$}
{
	#if {"%0" == "notrack"}
	{
		#unvariable {auto.track};
		hunt
	};
	#else
	{
		#regexp {%0} {track {.*}} {#variable {auto.track} {&1};hunt &1}
	}
}
{9}
