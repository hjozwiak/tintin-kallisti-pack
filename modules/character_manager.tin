#ALIAS {^CSAVE$}
{
	#if {"$MSDP_CHARACTER_NAME" != ""}
	{
		#history {write} {${HOME}/${MSDP_CHARACTER_NAME}\.history};
		#class {${MSDP_CHARACTER_NAME}} {write} {${HOME}/${MSDP_CHARACTER_NAME}};
		#class {${MSDP_CHARACTER_NAME}} {open}
	}
}
{9}

#ALIAS {^cmod{kill|load|reload} %S$}
{
	#nop {character module path};
	#variable {modpath} {${HOME}/${MSDP_CHARACTER_NAME}\.modules};
	#replace {modpath} {\\} {};
	#nop {reload all modules};
	#if {"%0" == "cmodreload all"}
	{
		#script {for module in $modpath/*.tin ; do echo "#class {$(basename "$module" .tin)} {kill};#class {character_$(basename "$module" .tin)} {read} {$module}";done};
		#echo {All character modules reloaded}
	};
	#else
	{
		#nop {Kill, load, or reload a specified module.};
		#if {"%1" == "{kill|reload}"}
		{
			#class {character_%2} {kill};
			#showme {Character module %2 has been killed.}
		};
		#if {"%1" == "{load|reload}"}
		{
			#showme {Loading character module %2.tin};
			#class {character_%2} {read} {$modpath/%2.tin};
			#class {$MSDP_CHARACTER_NAME} {open}
		}
	};
	#unvariable {modpath}
}
{9}

#EVENT {VARIABLE UPDATE MSDP_CHARACTER_NAME}
{
	#if {&{OLD_MSDP_CHARACTER_NAME} && "${OLD_MSDP_CHARACTER_NAME}" != "" && "$OLD_MSDP_CHARACTER_NAME" != "%1"}
	{
		#history {write} {${HOME}/${OLD_MSDP_CHARACTER_NAME}.history};
		#class {${MSDP_CHARACTER_NAME}} {write} {${HOME}/${OLD_MSDP_CHARACTER_NAME}};
		#class {${OLD_MSDP_CHARACTER_NAME}} {kill};
		#class {character_%*} {kill}
	};
	#nop {Make sure the character file exists so there's no error on read.};
	#system {if ! [[ -f "$HOME/%1" ]]; then if [[ "%1" != "" ]]; then echo "#nop" > "$HOME/%1";fi;fi};
	#variable {OLD_MSDP_CHARACTER_NAME} {%1};
	#history {read} {${HOME}/%1.history};
	#class {%1} {read} {${HOME}/%1};
	#nop {Load personal modules.};
	#script {if [[ -d "$HOME/%1.modules" ]]; then for module in $HOME/%1.modules/*.tin ; do echo "#class {character_$(basename "$module" .tin)} {read} {$module}";done;fi};
	#if {"${cspam}" == "true"}
	{
		#class {applied_spam_settings} {read} {modules/spam.rules};
		#echo {Client side spam filters applied.}
	};
	#class {%1} {open}
}

#TICKER           {CSAVE}  {CSAVE}  {60}
#VARIABLE         {old_MSDP}  {}
