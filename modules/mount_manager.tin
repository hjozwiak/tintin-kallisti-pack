#ACTION {%1 drags you off your mount!$}
{
     #nop {Hook name: dismounted};
     #nop {Hook args: none};
     #if {(&{character[fightRide]} && ${character[fightRide]}) || $MSDP_OPPONENT_NUMBER == 0}
     {
          mount ${character[mount]};
          #variable {mounting} {1}
     };
     #else
     {
          #list {onDeath} {add} {mount ${character[mount]};#var {mounting} {1}}
     };
     @do_hook{dismounted}
}
{9}

#ACTION {^Already ridden.$}
{
     #if {$mounting}
     {
          #math {mountNumber} {$mountNumber + 1};
          mount ${mountNumber}.${character[mount]}
     }
}
{9}

#ACTION {^Mount what?$}
{
     #unvariable {character[mount]};
     #variable {mounting} {0};
     #variable {mountNumber} {1}
}
{9}

#ACTION {^No one by that name around.$}
{
     #variable {tethering} {0};
     #variable {tetherNumber} {1};
     #variable {tetherMob} {}
}
{9}

#ACTION {^Tether time set to around 20 hours.$}
{
     #if {$tethering}
     {
          #math {tetherNumber} {$tetherNumber + 1};
          #send {tether ${tetherNumber}.${tetherMob}}
     }
}
{9}

#ACTION {^You dismount from %1.$}
{
     #if {${tetherable}}
     {
          #regexp {%1} {{.*} {.*}}
          {
               tether &2;#variable {character[mount]} {&2}
          };
          #else
          {
               tether %1;
               #variable {character[mount]} {%1}
          }
     }
}
{9}

#ACTION {^You hop on %1's back.$}
{
     #regexp {%1} {{.*} {.*}}
     {
          #variable {character[mount]} {&2}
     };
     #else
     {
          #variable {character[mount]} {%1}
     };
     #variable {mounting} {0};
     #variable {mountNumber} {1}
}
{9}

#ACTION {^You lose control and fall off of %1.$}
{
     #nop {Hook name: dismounted};
     #nop {Hook args: none};
     #if {(&{character[fightRide]} && ${character[fightRide]}) || $MSDP_OPPONENT_NUMBER == 0}
     {
          #regexp {%1} {{.*} {.*}}
          {
               stand;mount &2;#var {mounting} {1}
          };
          #else
          {
               stand;
               mount %1;
               #variable {mounting} {1}
          }
     };
     #else
     {
          #regexp {%1} {{.*} {.*}}
          {
               #list {onDeath} {add} {mount &2;#var {mounting} {1}}
          };
          #else
          {
               #list {onDeath} {add} {mount %1;#variable {mounting} {1}}
          }
     };
     @do_hook{dismounted}
}
{9}

#ACTION {^You try to ride %1, but you fall on your butt.$}
{
     #variable {mounting} {1};
     #variable {mountNumber} {1};
     stand;
     #regexp {%1} {{.*} {.*}}
     {
          mount &2
     };
     #else
     {
          mount %1
     }
}
{9}

#ACTION {^You {(?:stop resting and |wake and |stop meditating and )?}{stand up|rise to your feet}.$}
{
     #if {&{character[mount]} && !$mounting}
     {
          #variable {mounting} {1};
          mount ${character[mount]};
          #unvariable ${character[mount]}
     }
}
{9}


#ALIAS {^MOUNT_HEALTH_EVENT %d$}
{
     #if {&{MSDP_MOUNT_HEALTH_MAX} && {$MSDP_MOUNT_HEALTH_MAX} != {0} && {$MSDP_MOUNT_HEALTH_MAX} != {}}
     {
          #if {!&{character[audioprompt]}}
          {
               #variable {character[audioprompt]} {1}
          };
          #math {audioprompt[mounthealth]} {(%1 / $MSDP_MOUNT_HEALTH_MAX) *100.0};
          #if {${audioprompt[mounthealth]} < 100}
          {
               #format {audioprompt[mounthealth]} {%d} {${audioprompt[mounthealth]} - ${audioprompt[mounthealth]} % 10};
               #if {${audioprompt[mounthealth]} != ${audioprompt[oldMountHealth]} && ${character[audioprompt]} && ${MSP[SOUND]}}
               {
                    #if {&{mounthealth${audioprompt[mounthealth]}}}
                    {
                         SYNTH_PLAY mounthealth${audioprompt[mounthealth]};
                         #variable {audioprompt[oldMountHealth]} {${audioprompt[mounthealth]}}
                    }
               }
          }
     };
     #if {&{autoHealMountPercent}}
     {
          #if {(%1 / $MSDP_MOUNT_HEALTH_MAX) * 100.0 < ${autoHealMountPercent} && !${spam[healmount]}}
          {
               ${autoHealMount} ${character[mount]};
               spamset healmount 6
          }
     };
     #local {mhealth} {};
     #format {mhealth} {%d} {(%1 / $MSDP_MOUNT_HEALTH_MAX) * 100.0};
     @do_hook{mountHealthPercent;$mhealth}
}
{9}

#ALIAS {^MOUNT_STAMINA_EVENT %d$}
{
     #if {&{MSDP_MOUNT_STAMINA_MAX} && {$MSDP_MOUNT_STAMINA_MAX} != {0} && {$MSDP_MOUNT_STAMINA_MAX} != {}}
     {
          #if {!&{character[audioprompt]}}
          {
               #variable {character[audioprompt]} {1}
          };
          #math {audioprompt[mountstamina]} {(%1 / $MSDP_MOUNT_STAMINA_MAX) *100.0};
          #if {${audioprompt[mountstamina]} < 100}
          {
               #format {audioprompt[mountstamina]} {%d} {${audioprompt[mountstamina]} - ${audioprompt[mountstamina]} % 10};
               #if {${audioprompt[mountstamina]} != ${audioprompt[oldMountStamina]} && ${character[audioprompt]} && ${MSP[SOUND]}}
               {
                    #if {&{mountmovement${audioprompt[mountstamina]}}}
                    {
                         SYNTH_PLAY mountmovement${audioprompt[mountstamina]};
                         #variable {audioprompt[oldMountStamina]} {${audioprompt[mountstamina]}}
                    }
               }
          }
     };
     #local {mstamina} {};
     #format {mstamina} {%d} {(%1 / $MSDP_MOUNT_STAMINA_MAX) * 100.0};
     @do_hook{mountStaminaPercent;$mstamina}
}
{9}

#ALIAS {^tether %*}
{
     #variable {tethering} {1};
     #variable {tetherMob} {%1};
     #send {%0}
}
{9}

#ALIAS {^{mmv|mhp}$}
{
     #switch {{%0}}
     {
          #case {{mmv}}
          {
               show mount_stamina
          };
          #case {{mhp}}
          {
               show mount_health
          }
     }
}
{9}

#ALIAS {^{no|}fightride$}
{
     #if {{%1} == {}}
     {
          #showme {You will now attempt to remount while fighting.};
          #variable {character[fightRide]} {1}
     };
     #else
     {
          #showme {You will no longer attempt to remount while fighting.};
          #variable {character[fightRide]} {0}
     }
}
{9}



#EVENT {VARIABLE UPDATE MSDP_MOUNT_HEALTH}
{
     #if {{%1} != {} && &{MSDP_CHARACTER_NAME} && {${MSDP_CHARACTER_NAME}} != {} && !$copying && &{MSDP_MOUNT_HEALTH_MAX} && {$MSDP_MOUNT_HEALTH_MAX} != {0} && {$MSDP_MOUNT_HEALTH_MAX} != {}}
     {
          #line oneshot #ticker {mountHealth} {MOUNT_HEALTH_EVENT %1} {0.25}
     }
}

#EVENT {VARIABLE UPDATE MSDP_MOUNT_STAMINA}
{
     #if {{%1} != {} && &{MSDP_CHARACTER_NAME} && {${MSDP_CHARACTER_NAME}} != {} && !$copying && &{MSDP_MOUNT_STAMINA_MAX} && {$MSDP_MOUNT_STAMINA_MAX} != {0} && {$MSDP_MOUNT_STAMINA_MAX} != {}}
     {
          #line oneshot #ticker {mountStamina} {MOUNT_STAMINA_EVENT %1} {0.25}
     }
}



#VARIABLE         {mountNumber}  {1}
#VARIABLE         {mounting}  {0}
#VARIABLE         {tetherMob}  {}
#VARIABLE         {tetherNumber}  {1}
#VARIABLE         {tethering}  {0}

