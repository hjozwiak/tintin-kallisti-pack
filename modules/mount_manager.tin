#ACTION {%1 drags you off your mount!$}
{
	#list {onDeath} {add} {mount ${character[mount]};#var {mounting} {1}}
}
{9}

#ACTION {^Already ridden.$}
{
	#if {$mounting}
	{
		#math {mountNumber} {$mountNumber + 1};
		mount ${mountNumber}.${character[mount]}
	}
}
{9}

#ACTION {^Mount what?$}
{
	#unvariable {character[mount]};
	#variable {mounting} {0};
	#variable {mountNumber} {1}
}
{9}

#ACTION {^You dismount from %1.$}
{
	#if {${tetherable}}
	{
		#regexp {%1} {{.*} {.*}} {tether &2;#variable {character[mount]} {&2}};
		#else
		{
			tether %1;
			#variable {character[mount]} {%1}
		}
	}
}
{9}

#ACTION {^You hop on %1's back.$}
{
	#regexp {%1} {{.*} {.*}} {#variable {character[mount]} {&2}};
	#else
	{
		#variable {character[mount]} {%1}
	};
	#variable {mounting} {0};
	#variable {mountNumber} {1}
}
{9}

#ACTION {^You lose control and fall off of %1.$}
{
	#regexp {%1} {{.*} {.*}} {#list {onDeath} {add} {mount &2;#var {mounting} {1}}};
	#else
	{
		#list {onDeath} {add} {mount %1};
		#variable {mounting} {1}
	}
}
{9}

#ACTION {^You try to ride %1, but you fall on your butt.$}
{
	#variable {mounting} {1};
	stand;
	#regexp {%1} {{.*} {.*}} {mount &2};
	#else
	{
		mount %1
	}
}
{9}

#ACTION {^You {(?:stop resting and |wake and |stop meditating and )?}{stand up|rise to your feet}.$}
{
	#if {&{character[mount]} && !$mounting}
	{
		mount ${character[mount]};
		#unvariable ${character[mount]}
	}
}
{9}

#ALIAS {^{mmv|mhp}$}
{
	#switch {"%0"}
	{
		#case {"mmv"}
		{
			show mount_stamina
		};
		#case {"mhp"}
		{
			show mount_health
		}
	}
}
{1}

#VARIABLE         {mountNumber}  {1}
#VARIABLE         {mounting}  {0}
