#ALIAS {^vote$}
{
	#local {voteTime};
	#format {voteTime} {%T};
	#script {echo -n "$voteTime" > "$HOME/vote.dat"};
	#run {kallistivote} {$browser http://www.topmudsites.com/vote-kallistimud.html};
	#delay {1} {#send { \}}
}
{9}

#ALIAS {^last{v|vo|vot|vote}$}
{
	#local {currTime};
	#local {voteTime};
	#format {currTime} {%T};
	#script {voteTime} {[[ -r "$HOME/vote.dat" ]] || echo -n "0" > "$HOME/vote.dat";cat $HOME/vote.dat};
	#variable {voteTime} {${voteTime[1]}};
	#math {currTime} {$currTime - $voteTime};
	#echo {It's been %m hours and %m minutes since your last vote for kallisti.} {$currTime / 3600} {$currTime % 3600 / 60}
}
{9}

#EVENT {HOUR}
{
	#local {currTime};
	#local {hours};
	#local {voteTime};
	#format {currTime} {%T};
	#script {voteTime} {[[ -r "$HOME/vote.dat" ]] || echo -n "0" > "$HOME/vote.dat";cat $HOME/vote.dat};
	#variable {voteTime} {${voteTime[1]}};
	#math {hours} {($currTime - $voteTime) / 3600};
	#if {$hours >= 12}
	{
		SYNTH_PLAY warningSound;
		#list {channel[All]} {insert} {1} {It's been $hours hours since your last vote for Kallisti. Use 'vote' to vote now.};
		#echo {It's been $hours hours since your last vote for Kallisti. Use 'vote' to vote now.}
	}
}
