#ALIAS {^show %S$}
{
	#format {tmp} {%u} {%1};
	#if {&{MSDP_${tmp}}}
	{
		#variable {msg} {${MSDP_${tmp}} %1};
		#if {&{MSDP_${tmp}_PERM}}
		{
			#variable {msg} {${msg} of ${MSDP_${tmp}_PERM}}
		};
		#elseif {&{MSDP_${tmp}_MAX}}
		{
			#variable {msg} {${msg} of ${MSDP_${tmp}_MAX}}
		}
	};
	#else
	{
		#variable {msg} {Nothing to show}
	};
	#variable {msg} {${msg}\.};
	#echo {${msg}}
}
{9}

#ALIAS {^{bp|hp|mn|mv}$}
{
	#if {"%0" == "hp"}
	{
		show health
	};
	#if {"%0" == "mv"}
	{
		show stamina
	};
	#if {"%0" == "mn"}
	{
		show mana
	}
}
{9}

#ALIAS {^{noaudio|audio}prompt$}
{
	#if {"%0" == "audioprompt"}
	{
		#variable {character.audioprompt} {true};
		#showme {Audio prompt enabled.}
	};
	#else
	{
		#variable {character.audioprompt} {false};
		#showme {Audio prompt disabled.}
	}
}
{9}

#EVENT {VARIABLE UPDATE MSDP_HEALTH}
{
	#if {!&{character.audioprompt}}
	{
		#variable {character.audioprompt} {true}
	};
	#math {audioprompt.health} {(%1 / $MSDP_HEALTH_MAX) *100.0};
	#if {${audioprompt.health} < 100}
	{
		#format {audioprompt.health} {%d} {${audioprompt.health} - ${audioprompt.health} % 10};
		#if {${audioprompt.health} != ${audioprompt.oldHealth} && "${character.audioprompt}" == "true" && "${MSP.SOUND}" == "true"}
		{
			#if {&{health${audioprompt.health}}}
			{
				SYNTH_PLAY health${audioprompt.health};
				#variable {audioprompt.oldHealth} {${audioprompt.health}}
			}
		}
	};
	#nop {Use the autorecall command if hp is below 20%};
	#if {(%1 / $MSDP_HEALTH_MAX) *100.0 <= 20}
	{
		#if {"${spam.recall}" != "true"}
		{
			#if {&{auto.recall}}
			{
				breakout;
				${auto.recall};
				spamset recall 90
			}
		}
	};
	#nop {Use the autoheal self command if hp is below 50%};
	#if {(%1 / $MSDP_HEALTH_MAX) *100.0 <= 50}
	{
		#if {&{autoHealSelf}}
		{
			${autoHealSelf}
		}
	}
}

#EVENT {VARIABLE UPDATE MSDP_MANA}
{
	#math {audioprompt.mana} {(%1 / $MSDP_MANA_MAX) *100.0};
	#if {${audioprompt.mana} < 100}
	{
		#format {audioprompt.mana} {%d} {${audioprompt.mana} - ${audioprompt.mana} % 10};
		#if {${audioprompt.mana} != ${audioprompt.oldMana}}
		{
			#if {"${character.audioprompt}" == "true" && "${MSP.SOUND}" == "true"}
			{
				#if {&{mana${audioprompt.mana}}}
				{
					SYNTH_PLAY mana${audioprompt.mana};
					#variable {audioprompt.oldMana} {${audioprompt.mana}}
				}
			}
		}
	}
}

#EVENT {VARIABLE UPDATE MSDP_OPPONENT_HEALTH}
{
	#if {!&{character.beeps}}
	{
		#variable {character.beeps} {true}
	};
	#if {%1 != 0 && "${character.beeps}" == "true" && "${MSP.SOUND}" == "true"}
	{
		#math {enemyConditionTone} {%1 * 10 + 200};
		#system {play -q -V0 -n synth 0.06 tri $enemyConditionTone fade 0.01 0.09 gain $soundVolume&}
	}
}

#EVENT {VARIABLE UPDATE MSDP_STAMINA}
{
	#math {audioprompt.movement} {(%1 / $MSDP_STAMINA_MAX) *100.0};
	#if {${audioprompt.movement} < 100}
	{
		#format {audioprompt.movement} {%d} {${audioprompt.movement} - ${audioprompt.movement} % 10};
		#if {${audioprompt.movement} != ${audioprompt.oldMovement}}
		{
			#if {"${character.audioprompt}" == "true" && "${MSP.SOUND}" == "true"}
			{
				#if {&{movement${audioprompt.movement}}}
				{
					SYNTH_PLAY movement${audioprompt.movement};
					#variable {audioprompt.oldMovement} {${audioprompt.movement}}
				}
			}
		}
	}
}

#VARIABLE         {audioprompt}  {{oldHealth}{0}{oldMana}{0}{oldMovement}{0}}
