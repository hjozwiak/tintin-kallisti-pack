#ACTION {%1 diverts its attention to %w.$}
{
	#list {autorescueList} {find} {%2} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	}
}
{9}

#ACTION {%1 flies from your grasp.$}
{
	gt OH GODS NOOOO! I have been disarmed!;
	rem buckler;
	#regexp {%1} {{.*} {.*}} {get &2;wield &2};
	#else
	{
		get %1;
		wield %1
	};
	wear buckler
}
{9}

#ACTION {%1 hits %w {}{\.|!}$}
{
	#list {autorescueList} {find} {%2} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]};
		spamset rescue%2
	}
}
{9}

#ACTION {^%w attempts to backstab %2, but flies right through {her|him|it}.$}
{
	#list {autorescueList} {find} {%2} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	}
}
{9}

#ACTION {^%w is {moderately hurt|severely hurt|near death}.$}
{
	#list {autohealList} {find} {%1} {i};
	#if {$i != 0}
	{
		#switch {"${MSDP_CLASS}"}
		{
			#case {Paladin}
			{
				#if {$OPPONENT_HEALTH == 0}
				{
					lay %1
				};
				#else
				{
					#list {onDeath} {add} {lay %1}
				}
			};
			#default
			{
				HEAL %1
			}
		}
	};
	#list {autorescueList} {find} {%1} {i};
	#if {$i != 0}
	{
		Rescue %1
	}
}
{9}

#ACTION {^%w places something%*}
{
	#list {autorescueList} {find} {%2} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	}
}
{9}

#ACTION {^%w tries to backstab %2 but nearly cuts {her|his|its} own finger!$}
{
	#list {autorescueList} {find} {%2} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	}
}
{9}

#ACTION {^Saving %w.$}
{
	#if {"${spam.save}" == "true"}
	{
		#line {gag}
	}
}
{9}

#ACTION {^You receive your reward for the kill, %d experience poin{t|ts}.}
{
	#if {"${battle_manager.tankName}" != ""}
	{
		reform ${battle_manager.tankName} fr;
		march ${battle_manager.tankName}
	};
	#nop {Do anything that needs to be scheduled on death.};
	#if {"${onDeath}" != ""}
	{
		#forall {${onDeath[]}}{${onDeath[&0]}};
		#list {onDeath} {clear}
	};
	look in corpse #nop {Save after every battle.};
	#if {!&{spam.save}}
	{
		save;
		spamset save
	}
}
{9}

#ACTION {^You take %w's position in the formation.$}
{
	rescue %1
}
{9}

#ACTION {^You try but are unable to head off {her|his|its} opponent.$}
{
	#forall {${autorescueList[]}}{rescue ${autorescueList[&0]}}
}
{9}

#ACTION {^You {now follow|stop following} %w.$}
{
	#if {"%1" == "stop following"}
	{
		#unvariable {leader}
	};
	#else
	{
		#variable {leader} {%2}
	}
}
{9}

#ALIAS {^auto{heal|rescue} %s{add|remove} %s%w$}
{
	#format {battle_manager.tmp} {%n} {%5};
	#list {auto%1List} {find} {${battle_manager.tmp}} {i};
	#if {"%3" == "remove"}
	{
		#if {$i == 0}
		{
			#echo {You are not current automatically %1ing ${battle_manager.tmp}\.}
		};
		#else
		{
			#list {auto%1List} {delete} {$i};
			#echo {You are no longer automatically %1ing ${battle_manager.tmp}\.}
		}
	};
	#else
	{
		#if {$i != 0}
		{
			#echo {You are already automatically %1ing ${battle_manager.tmp}\.}
		};
		#else
		{
			#list {auto%1List} {add} {${battle_manager.tmp}};
			#echo {You are now automatically %1ing ${battle_manager.tmp}\.}
		}
	}
}
{9}

#ALIAS {^auto{heal|rescue}$}
{
	#echo {You are currently automatically %1ing:};
	#list {auto%1List} {size} {i};
	#if {$i > 0}
	{
		#forall {${auto%1List[]}}{#echo {${auto%1List[&0]}}}
	}
}
{9}

#ALIAS {^noauto{heal|rescue}$}
{
	#list {auto%1List} {clear};
	#echo {You are no longer %1ing anyone.}
}
{9}

#ALIAS {^{notank|tank|tank \w+}$}
{
	#if {"%0" == "notank"}
	{
		#variable {battle_manager.tankName} {};
		#echo {Tank cleared.}
	};
	#else
	{
		#regexp {%0} {{.*} {.*}} {#variable {battle_manager.tankName} {&2}};
		#else
		{
			#variable {battle_manager.tankName} {self}
		};
		#echo {tank set to ${battle_manager.tankName}\.}
	}
}
{9}

#EVENT {VARIABLE UPDATE MSDP_OPPONENT_NAME}
{
	#if {"%1" != ""}
	{
		#variable {OLD_MSDP_OPPONENT_NAME} {%1}
	}
}

#VARIABLE         {DAMAGE}  {tickle|barely hit|hit|hard|very hard|extremely hard|massacre to small fragments|obliterate|annihilate|mutilate|immolate|liquify|vaporize|critically vaporize}
#VARIABLE         {autohealList}  {}
#VARIABLE         {autorescueList}  {}
#VARIABLE         {battle_manager}  {}
#VARIABLE         {onDeath}  {}
