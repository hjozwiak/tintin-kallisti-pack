#action {^You take %w's position in the formation.$}
{
rescue %1
}
{9}

#action {^%w attempts to backstab %2, but flies right through {her|him|it}.$}
{
#if {"%1" == "{${autorescueList}}"}
{
Rescue %1
}
}
{9}

#action {^%w is {moderately hurt|severely hurt|near death}.$}
{
#if {"%1" == "{${autorescueList}}"}
{
Rescue %1
}
}
{9}

#action {^%w tries to backstab %2 but nearly cuts {her|his|its} own finger!$}
{
#if {"%1" == "{${autorescueList}}"}
{
Rescue %1
}
}
{9}

#action {^You try but are unable to head off {her|his|its} opponent.$}
{
#if {"${autorescueList}" != ""}
{
#forall {$autorescueList}
{
rescue &0
}
}
}
{9}

#action {%1 flies from your grasp.$}
{
gt OH GODS NOOOO! I have been disarmed!;
rem buckler;
#regexp {%1} {{.*} {.*}} {get &2;wield &2} {get %1;wield %1};
wear buckler
}
{9}

#action {^%w places something%*}
{
#if {"%1" == "{${autorescueList}}"}
{
rescue %1
}
}
{9}

#alias {^{noautorescue|autorescue .+}$}
{
#if {"%0" == "noautorescue"}
{
#echo {You will no longer automatically rescue anyone.};
#variable {autorescueList} {}
};
#else
{
#variable {autorescueList} {};
#regexp {%0} {{\w+} {.*}} {#variable {tmp.battle_manager} {&2}};
#replace {tmp.battle_manager} { } {;};
#forall {${tmp.battle_manager}} {#format {i} {%n} {&0};#variable {autorescueList} {${autorescueList} $i}};
#format {autorescueList} {%p} {$autorescueList};
#echo {You are automatically rescuing: ${autorescueList}};
#replace {autorescueList} { } {|}
}
}
{9}

#action {%1 hits %w {${DAMAGE}}{\.|!}$}
{
#if {"%2" == "{${autorescueList}}"}
{
#if {!&{spam.rescue%2}}
{
rescue %2;
spamset rescue%2
}
}
}
{9}

#action {^You receive your reward for the kill, %d experience poin{t|ts}.$}
{
#if {"${tankName}" != ""}
{
reform $tankName fr
};
#nop {Do anything that needs to be scheduled on death.};
#if {"${onDeath}" != ""}
{
#forall {${onDeath[]}}
{
${onDeath[&0]}
};
#list {onDeath} {clear}
};
look in corpse
#nop {Save after every battle.};
save
}
{9}

#alias {^{notank|tank|tank \w+}$}
{
#if {"%0" == "notank"}
{
#variable {tankName} {}
};
#else
{
#regexp {%0} {{.*} {.*}} {#variable {tankName} {&2}} {#variable {tankName} {me}}
}
}
{9}
#variable {tankName} {}
#variable {DAMAGE} {tickle|barely hit|hit|hard|very hard|extremely hard|massacre to small fragments|obliterate|annihilate|mutilate|immolate|liquify|vaporize|critically vaporize}
#variable {autorescueList} {}
#variable {onDeath} {}
