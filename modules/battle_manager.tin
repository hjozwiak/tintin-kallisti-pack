#ACTION {%1 charges madly into %2, but can't budge {her|him|it}.$}
{
	#list {autorescueList} {size} {i};
	#if {$i > 0}
	{
		#forall {%1;%2}{#list {autorescueList} {find} {&0} {i};#if {$i != 0}{rescue ${autorescueList[$i]}}}
	};
	#list {groupMemberList} {find} {%1} {i};
	#if {$i > 0}
	{
		join %1
	};
	#list {groupMemberList} {find} {%2} {i};
	#if {$i > 0}
	{
		join %2
	}
}
{9}

#ACTION {%1 diverts {her|his|its} attention to %w.$}
{
	#list {autorescueList} {find} {%2} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	}
}
{9}

#ACTION {%1 flies from your grasp.$}
{
	#regexp {%1} {{.*} {.*}} {get &2;remove buckler;wield &2};
	#else
	{
		get %1;
		remove buckler;
		wield %1
	};
	wear buckler;
	gt &byO&brH &0cG&brO&byD&0cS &brN&byO&brO&byO&brO&bw! &0eI &bgh&bma&bgv&bme &bgb&bye&bre&bgn &brDISARMED!
}
{9}

#ACTION {%1 hits %w %*{\.|!}$}
{
	#list {autorescueList} {find} {%2} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]};
		spamset rescue%2
	}
}
{9}

#ACTION {%1 picks you up by the neck and throws you out of the room.$}
{
	#nop {Hook name is roomTossed	};
	#nop {Hook args: name of thing that tossed you.};
	#list {roomTossed} {size} {i};
	#if {$i > 0}
	{
		#forall {${roomTossed[]}} {@${roomTossed[&0]}{%1}}
	}
}
{9}

#ACTION {%1 places something in the back of %2, resulting in a gush of blood!$}
{
	#list {autorescueList} {find} {%1} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	};
	#list {groupMemberList} {find} {%1} {i};
	#if {$i > 0}
	{
		#if {${auto.charge} && !${MSDP_OPPONENT_NUMBER} && $MSDP_STAMINA >= 300}
		{
			charge %2
		};
		#else
		{
			join %1
		}
	};
	#list {groupMemberList} {find} {%2} {i};
	#if {$i > 0}
	{
		#if {${auto.charge} && !${MSDP_OPPONENT_NUMBER} && $MSDP_STAMINA > 300}
		{
			charge %1
		};
		#else
		{
			join %2
		}
	}
}
{9}

#ACTION {%1 screams insanely and charges into %2!$}
{
	#list {autorescueList} {size} {i};
	#if {$i > 0}
	{
		#forall {%1;%2}{#list {autorescueList} {find} {&0} {i};#if {$i != 0}{rescue ${autorescueList[$i]}}}
	};
	#list {groupMemberList} {find} {%1} {i};
	#if {$i > 0}
	{
		join %1
	};
	#list {groupMemberList} {find} {%2} {i};
	#if {$i > 0}
	{
		join %2
	}
}
{9}

#ACTION {%1 trips as {he|it|she} charges into %3!$}
{
	#list {autorescueList} {size} {i};
	#if {$i > 0}
	{
		#forall {%1;%3}{#list {autorescueList} {find} {&0} {i};#if {$i != 0}{rescue ${autorescueList[$i]}}}
	};
	#else
	{
		#list {groupMemberList} {find} {%1} {i}
	};
	#if {$i > 0}
	{
		join %1
	};
	#list {groupMemberList} {find} {%3} {i};
	#if {$i > 0}
	{
		join %3
	}
}
{9}

#ACTION {^%S heals %w's wounds.$}
{
	#list {autorescueList} {find} {%2} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	}
}
{9}

#ACTION {^%S stops following %w.$}
{
	#list {groupMemberList} {find} {%1} {i};
	#if {$i > 0}
	{
		#list {groupMemberList} {delete} {$i}
	}
}
{9}

#ACTION {^%s%w %s[%S%s%d]%s(%*}
{
	#list {groupMemberList} {find} {%2} {i};
	#if {$i == 0}
	{
		#list {groupMemberList} {add} {%2}
	};
	#regexp {%0} {{.*\x7BLeader\x7D} {#variable {leader} {%2}}
}
{9}

#ACTION {^%w attempts to backstab %2, but flies right through {her|him|it}.$}
{
	#list {autorescueList} {find} {%1} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	};
	#list {groupMemberList} {find} {%1} {i};
	#if {$i > 0}
	{
		#if {${auto.charge} && !${MSDP_OPPONENT_NUMBER} && $MSDP_STAMINA > 300}
		{
			charge %2
		};
		#else
		{
			join %1
		}
	};
	#list {groupMemberList} {find} {%2} {i};
	#if {$i > 0}
	{
		#if {${auto.charge} && !${MSDP_OPPONENT_NUMBER} && $MSDP_STAMINA > 300}
		{
			charge %1
		};
		#else
		{
			join %2
		}
	}
}
{9}

#ACTION {^%w is now a group member %*.$}
{
	#list {groupMemberList} {find} {%1} {i};
	#if {$i == 0}
	{
		#list {groupMemberList} {add} {%1}
	}
}
{9}

#ACTION {^%w is {moderately hurt|severely hurt|near death|incapacitated .*|stunned .*|mortally wounded .*}.$}
{
	#list {autohealList} {find} {%1} {i};
	#if {$i != 0}
	{
		#switch {"${MSDP_CLASS}"}
		{
			#case {Paladin}
			{
				#forall {moderate;severe;nearDeath;incapacitated;;stunned;morted}{#if {!&{heal.&0}}{#variable {heal.&0} {lay}}}
			};
			#default
			{
				#forall {moderate;severe;nearDeath;incapacitated;stunned;morted}{#if {!&{heal.&0}}{#variable {heal.&0} {heal}}}
			}
		};
		#switch {"%2"}
		{
			#case {"moderately hurt"}
			{
				${heal.moderate} %1
			};
			#case {"severely hurt"}
			{
				${heal.severe} %1
			};
			#case {"near death"}
			{
				${heal.nearDeath} %1
			};
			#case {"incapacitated%*"}
			{
				${heal.incapacitated} %1
			};
			#case {"stunned%*"}
			{
				${heal.stunned} %1
			};
			#case {"mortally wounded%*"}
			{
				${heal.morted} %1
			}
		}
	};
	#list {autorescueList} {find} {%1} {i};
	#if {$i != 0}
	{
		Rescue %1
	}
}
{9}

#ACTION {^%w tries to backstab %2 but nearly cuts {her|his|its} own finger!$}
{
	#list {autorescueList} {find} {%1} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	};
	#list {groupMemberList} {find} {%1} {i};
	#if {$i > 0}
	{
		#if {${auto.charge} && !${MSDP_OPPONENT_NUMBER} && $MSDP_STAMINA > 300}
		{
			charge %2
		};
		#else
		{
			join %1
		}
	};
	#list {groupMemberList} {find} {%2} {i};
	#if {$i > 0}
	{
		#if {${auto.charge} && !${MSDP_OPPONENT_NUMBER} && $MSDP_STAMINA > 300}
		{
			charge %1
		};
		#else
		{
			join %2
		}
	}
}
{9}

#ACTION {^Looks like its not quite %1's bedtime.$}
{
	#list {autorescueList} {size} {i};
	#if {$i > 0}
	{
		#forall {${autorescueList[]}}{rescue ${autorescueList[&0]}}
	}
}
{9}

#ACTION {^SEEK SHELTER AT ONCE!%s%w has begun to sing.$}
{
	#list {autorescueList} {find} {%1} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	}
}
{9}

#ACTION {^Saving %w.$}
{
	#if {${spam.save}}
	{
		#line {gag}
	}
}
{9}

#ACTION {^You join the fight with %w.$}
{
	#list {autorescueList} {find} {%1} {i};
	#if {$i != 0}
	{
		rescue ${autorescueList[$i]}
	}
}
{9}

#ACTION {^You receive your reward for the kill, %1}
{
	#nop {Hook name is mobDeath.};
	#nop {Hook args all text for rewoard for the kill.};
	#if {"${onDeath}" != ""}
	{
		#forall {${onDeath[]}}{${onDeath[&0]}};
		#list {onDeath} {clear}
	};
	look in corpse;
	#if {!&{spam.save}}
	{
		save;
		spamset save
	};
	#list {mobDeath} {size} {i};
	#if {$i > 0}
	{
		#forall {${mobDeath[]}} {@${mobDeath[&0]}{%1}}
	}
}
{9}

#ACTION {^You take %w's position in the formation.$}
{
	rescue %1
}
{9}

#ACTION {^You try but are unable to head off {her|his|its} opponent.$}
{
	#forall {${autorescueList[]}}{rescue ${autorescueList[&0]}}
}
{9}

#ACTION {^You {now follow|stop following} %w.$}
{
	#if {"%1" == "stop following"}
	{
		#list {groupMemberList} {clear};
		#unvariable {leader}
	};
	#else
	{
		#variable {leader} {%2}
	}
}
{9}

#ALIAS {^auto{heal|rescue} {add|remove} {all|group}$}
{
	#list {auto%1List} {clear};
	#if {"%2" == "remove"}
	{
		#echo {You are no longer %1ing anyone.}
	};
	#else
	{
		#nop {Populate the groupMemberList variable};
		group;
		#nop {The actual list management takes place in a POPULATE_whatever_LIST alias called with localecho so we know all the group members have been added.};
		localecho silent_do POPULATE_%1_LIST
	}
}
{8}

#ALIAS {^POPULATE_%w_LIST$}
{
	#list {groupMemberList} {size} {i};
	#if {$i > 0}
	{
		#forall {${groupMemberList[]}}{#if {"${groupMemberList[&0]}" != "${MSDP_CHARACTER_NAME}"}{auto%1 add ${groupMemberList[&0]}}}
	};
	auto%1
}
{9}

#ALIAS {^auto{heal|rescue} %s{add|remove} %s%w$}
{
	#format {battle_manager.tmp} {%n} {%5};
	#list {auto%1List} {find} {${battle_manager.tmp}} {i};
	#if {"%3" == "remove"}
	{
		#if {$i == 0}
		{
			#echo {You are not current automatically %1ing ${battle_manager.tmp}\.}
		};
		#else
		{
			#list {auto%1List} {delete} {$i};
			#echo {You are no longer automatically %1ing ${battle_manager.tmp}\.}
		}
	};
	#else
	{
		#if {$i != 0}
		{
			#echo {You are already automatically %1ing ${battle_manager.tmp}\.}
		};
		#else
		{
			#list {auto%1List} {add} {${battle_manager.tmp}};
			#echo {You are now automatically %1ing ${battle_manager.tmp}\.}
		}
	}
}
{9}

#ALIAS {^auto{heal|rescue}$}
{
	#echo {You are currently automatically %1ing:};
	#list {auto%1List} {size} {i};
	#if {$i > 0}
	{
		#forall {${auto%1List[]}}{#echo {${auto%1List[&0]}}}
	};
	#if {&{autoHealSelf}}
	{
		#echo {Yourself with: ${autoHealSelf} at ${autoHealSelfPercent} percent.}
	}
}
{9}

#ALIAS {^flee}
{
	#if {$MSDP_OPPONENT_NUMBER > 0}
	{
		#send {%0}
	}
}
{9}

#ALIAS {^he{a|al} %sall$}
{
	#list {autohealList} {size} {i};
	#if {$i == 0}
	{
		#echo {No one to heal}
	};
	#else
	{
		#forall {${autohealList[]}}{he%1 ${autohealList[&0]}}
	}
}
{9}

#ALIAS {^noauto{heal|rescue}$}
{
	#list {auto%1List} {clear};
	#echo {You are no longer %1ing anyone.}
}
{9}

#ALIAS {^res{c|cu|cue} %sall$}
{
	#list {autorescueList} {size} {i};
	#if {$i == 0}
	{
		#echo {No one to rescue}
	};
	#else
	{
		#forall {${autorescueList[]}}{res%1 ${autorescueList[&0]}}
	}
}
{9}

#ALIAS {^{notank|tank|tank \w+}$}
{
	#if {"%0" == "notank"}
	{
		#variable {battle_manager.tankName} {};
		#echo {Tank cleared.}
	};
	#else
	{
		#regexp {%0} {{.*} {.*}} {#variable {battle_manager.tankName} {&2}};
		#else
		{
			#variable {battle_manager.tankName} {self}
		};
		#echo {tank set to ${battle_manager.tankName}\.}
	}
}
{9}

#EVENT {VARIABLE UPDATE MSDP_OPPONENT_NAME}
{
	#if {"%1" != ""}
	{
		#variable {OLD_MSDP_OPPONENT_NAME} {%1};
		#if {"$MSDP_CLASS" == "Necromancer"}
		{
			#if {$MSDP_LEVEL >= 16}
			{
				order followers assist $MSDP_CHARACTER_NAME
			}
		}
	}
}

#VARIABLE         {DAMAGE}  {tickle|barely hit|hit|hard|very hard|extremely hard|massacre to small fragments|obliterate|annihilate|mutilate|immolate|liquify|vaporize|critically vaporize}
#VARIABLE         {autohealList}  {}
#VARIABLE         {autorescueList}  {}
#VARIABLE         {battle_manager}  {}
#VARIABLE         {groupMemberList}  {}
#VARIABLE         {onDeath}  {}
