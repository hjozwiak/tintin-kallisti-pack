#ACTION {^!!SOUND(%1)$}
{
	#variable {MSP.LAST} {%1};
	PLAY %1
}
{9}

#ACTION {^Audible sound is {ON|OFF}.$}
{
	#variable {MSP.SOUND} {%1};
	#replace {MSP.SOUND} {ON} {1};
	#replace {MSP.SOUND} {OFF} {0}
}
{9}

#ALIAS {PLAY %S}
{
#if {!&{soundVolume}}
{
#variable {soundVolume} {0}
};
	#if {!&{MSP.SOUND}}
	{
		#variable {MSP.SOUND} {1}
	};
	#if {${MSP.SOUND}}
	{
		#line {substitute} {escape} #system {play -qV0 "$($shuf -n1 -e ${HOME}/sounds/%1)" gain $soundVolume &> /dev/null &}
	}
}
{9}

#ALIAS {^MSP$}
{
	#if {!&{MSP.PROTOCOL}}
	{
		#variable {MSP.PROTOCOL} {not enabled}
	};
	#echo {MSP is ${MSP.PROTOCOL}\.}
}
{9}

#ALIAS {^SYNTH_PLAY %1}
{
#if {!&{soundVolume}}
{
#variable {soundVolume} {0}
};
	#if {!&{MSP.SOUND}}
	{
		#variable {MSP.SOUND} {1}
	};
	#if {${MSP.SOUND}}
	{
		#if {&{%1}}
		{
			#system {play -qV0 ${%1} gain $soundVolume &> /dev/null &}
		}
	}
}
{9}

#EVENT {IAC WILL MSP}
{
	#send {\xFF\xFD\x5A\};
	#variable {MSP.PROTOCOL} {enabled}
}

#GAG              {^!!SOUND%*}
#MACRO {\e>}
{
	#if {$volume <= -40}
	{
		#variable {soundVolume} {-40}
	};
	#else
	{
		#math {soundVolume} {$soundVolume - 2}
	};
	SYNTH_PLAY newVolumeSound
}

#MACRO {\e?}
{
	#if {$soundVolume >= 0}
	{
		#variable {soundVolume} {0}
	};
	#else
	{
		#math {soundVolume} {$soundVolume + 2}
	};
	SYNTH_PLAY newVolumeSound
}
