#ALIAS {^autocombat %1 %d$}
{
	#list {autocombatList} {clear};
	#list {autocombatList} {add} {@strip_braces{%1}};
	autocombat;
	#echo {Will execute every %2 seconds.};
	@createAutocombatTicker{%2};
	CSAVE
}
{8}

#ALIAS {^autocombat %1}
{
	#list {autocombatList} {clear};
	#list {autocombatList} {add} {@strip_braces{%1}};
	autocombat;
	#echo {Will execute every 7 seconds.};
	@createAutocombatTicker{7};
	CSAVE
}
{9}

#ALIAS {^autocombat$}
{
	#local {i} {};
	#echo {Automatic combat:};
	#if {"${autocombatList}" != ""}
	{
		#foreach {${autocombatList[]}} {i}
		{
			#echo {${autocombatList[$i]}}
		}
	};
	#echo {To set your autocombat commands, type autocombat follow by a list of commands enclosed in braces and separated by semi-colons.}
}
{9}

#ALIAS {^noautocombat$}
{
	#list {autocombatList} {clear};
	#echo {automatic combat disabled.};
	#unticker {auto_combat};
	CSAVE
}
{9}

#FUNCTION {createAutocombatTicker}
{
	#if {!&{autoHealSelfPercent}}
	{
		#variable {autoHealSelfPercent} {30}
	};
	#ticker {auto_combat}  {#if {$MSDP_OPPONENT_NUMBER != 0 && @percent{MSDP_HEALTH} > ${autoHealSelfPercent}}{#if {"${autocombatList}" != ""}{${autocombatList[@list_rand{autocombatList}]}}}}                                                                                            {%1};
	#return #nop
}
