#ALIAS {^CREATE_MACRO %.$}
{
	#macro {\e%1} {#if {&keyProfile[${keySetName}][%1]}{$keyProfile[${keySetName}][%1]}}
}
{9}

#ALIAS {^set{a|b|c|d|e|f|g|h|n|q|r|s|t|v|w|x|y|z|A|D|F|H|Q|W|E} %2}
{
	#if {!&{keySetName}}
	{
		#variable {keySetName} {Default}
	};
	#variable {keyProfile[${keySetName}][%1]} {@strip_braces{%2}};
	set%1;
	CSAVE
}
{9}

#ALIAS {^set{a|b|c|d|e|f|g|h|n|q|r|s|t|v|w|x|y|z|A|D|F|H|Q|W|E}$}
{
	#if {!&{keySetName}}
	{
		#variable {keySetName} {Default}
	};
	#if {&{keyProfile[${keySetName}][%1]}}
	{
		#showme {alt+%1 is set to ${keyProfile[${keySetName}][%1]}\.}
	};
	#else
	{
		#showme {alt+%1 is not set.}
	}
}
{9}

#ALIAS {^{ke|key|keyp|keypr|keypro|keyprof|keyprofi|keyprofil|keyprofile|kp|kpr|kpro|kprof|kprofi|kprofil|kprofile|ks|kse|kset|keys|keyse|keyset} %s%S$}
{
	#format {keySetName} {%n} {%3};
	#if {!&{keyProfile[${keySetName}]}}
	{
		#showme {keyset ${keySetName} (new).}
	};
	#else
	{
		#showme {keyset ${keySetName}\.}
	};
	CSAVE
}
{9}

#ALIAS {^{ke|key|keyp|keypr|keypro|keyprof|keyprofi|keyprofil|keyprofile|kp|kpr|kpro|kprof|kprofi|kprofil|kprofile}$}
{
	#if {!&{keySetName}}
	{
		#variable {keySetName} {Default}
	};
	#showme {Keyset $keySetName\.}
}
{9}

#FUNCTION {generate_macros}
{
	#local {i} {};
	#foreach {a;b;c;d;e;f;g;h;n;q;r;s;t;v;w;x;y;z;A;D;F;H;Q;W;E} {i}
	{
		CREATE_MACRO $i
	};
	#return #nop
}

#MACRO {\cc}
{
	breakout
}

#MACRO {\e[2~}
{
	#buffer {home}
}

#MACRO {\e[3~}
{
	#buffer {end}
}

@generate_macros{}
