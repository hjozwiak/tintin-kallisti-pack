#ACTION {^You get %1 from the corpse.$}
{
	#nop {Hook name: lootCorpse};
	#nop {Hook args: full text of looted item, e.g. a longsword.};
	#nop {Only call hook for non-coin related looting.};
	#local {i} {0};
	#local {j} {};
	#if {"%1" != "%*of gold coins" }
	{
		#list {hook[lootCorpse]} {size} {i};
		#if {$i > 0}
		{
			#foreach {*{hook[lootCorpse][]}} {j}
			{
				@$j{%1}
			}
		}
	}
}
{9}

#ALIAS {^{get|give|put} %2, %w %w$}
{
	#local {i} {};
	#local {itemDestination} {%4};
	#local {items} {%2, %3};
	#replace {itemDestination} {^{away|out|up}$} {${character[container]}};
	#replace {items} {,} {;};
	#foreach {$items} {i}
	{
		%1 $i $itemDestination
	}
}
{8}

#ALIAS {^de{q|qu|qui|quip|quipm|quipme|quipmen|quipment}$}
{
	equipment condition
}
{9}

#ALIAS {^{buy|donate|drop|get|give|take} %2, %3}
{
	#local {i} {};
	#local {items} {%2, %3};
	#replace {items} {,} {;};
	#foreach {$items} {i}
	{
		%1 $i
	}
}
{9}

#ALIAS {^{get|put|take}%s %3 {away|out|up}$}
{
	%1 %3 ${character[container]}
}
{9}
