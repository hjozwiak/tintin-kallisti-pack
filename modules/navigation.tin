#ACTION {The %1 {above|below|to the .*} seems to be closed.}
{
	#variable {tmp} {%2};
	#replace {tmp} {above} {up};
	#replace {tmp} {below} {down};
	#replace {tmp} {to the } {};
	#regexp {%1} {{.*} {.*}} {open &2 $tmp};
	#else
	{
		open %1 $tmp
	};
	#if {"${auto.track}" != ""}
	{
		look
	}
}
{9}

#ACTION {^%w enters %2.$}
{
	#if {"%1" == "$leader"}
	{
		#regexp {%2} {{.*} {.*}} {enter &2};
		#else
		{
			enter %2
		}
	}
}
{9}

#ACTION {^Alas, you cannot go {down|east|north|south|up|west}...$}
{
	SYNTH_PLAY cannot_go_%1
}
{9}

#ACTION {^The lock springs open.$}
{
	#if {"${auto.track}" != ""}
	{
		look
	}
}
{9}

#ACTION {^You seem to be unable to pick this lock.$}
{
	#unvariable {auto.track}
}
{9}

#EVENT {VARIABLE UPDATE MSDP_ROOM_EXITS}
{
	#forall {%1}{#if {"&0" == "{down|east|north|south|up|west}"}{#format {i} {%n} {&0}};#if {"&0" == "C"}{SYNTH_PLAY door$i}}
}

#EVENT {VARIABLE UPDATE MSDP_ROOM_NAME}
{
	#nop {Hook name: roomName};
	#nop {Hook args: the rooms name.};
	#list {roomName} {size} {i};
	#if {$i > 0}
	{
		#forall {${roomName[]}} {@${roomName[&0]}{%1}}
	}
}

#EVENT {VARIABLE UPDATE MSDP_ROOM_VNUM}
{
	#nop {Hook name: roomNumber};
	#nop {Hook args: the rooms vnum.};
	#switch {%1}
	{
		#case {3546}
		{
			#nop {at Midgaard bank, deposit all gold if autodeposit is enabled.};
			#if {${auto[deposit]}}
			{
				#if {$MSDP_GOLD > 0}
				{
					deposit all
				}
			}
		}
	};
	#list {roomNumber} {size} {i};
	#if {$i > 0}
	{
		#forall {${roomNumber[]}} {@${roomNumber[&0]}{%1}}
	}
}

#MACRO {\e,}
{
	s
}

#MACRO {\e<}
{
	down
}

#MACRO {\eI}
{
	up
}

#MACRO {\eK}
{
	#ignore {sub} {on};
	scan;
	localecho silent_do #ignore sub off
}

#MACRO {\ei}
{
	n
}

#MACRO {\ej}
{
	w
}

#MACRO {\ek}
{
	scan
}

#MACRO {\el}
{
	e
}
