#ACTION {%1 {flies|leaves|swims} {down|east|north|south|up|west}.$}
{
	#nop {Hook name: leave};
	#nop {Hook args: flee status 0 for no, 1 for yes.};
	#nop {Hook args: name of leaveee.};
	#nop {Hook args: direction they left.};
	#local {i} {};
	#local {j} {};
	#if {"%1" == "${fleeString}"}
	{
		#variable {fleeString} {1}
	};
	#else
	{
		#variable {fleeString} {0}
	};
	#list {hook[leave]} {size} {i};
	#if {$i > 0}
	{
		#foreach {*{hook[leave][]}} {j}
		{
			@${j}{${fleeString};%1;%3}
		}
	};
	#variable {fleeString} {}
}
{9}

#ACTION {The %1 {above|below|to the .*} seems to be closed.}
{
	#local {i} {%2};
	#replace {i} {above} {up};
	#replace {i} {below} {down};
	#replace {i} {to the } {};
	#line substitute variables #regexp {%1} {{.*} {.*}} {open &2 $i};
	#line substitute variables #else {open %1 $i};
	#if {"${auto[track]}" != ""}
	{
		look
	}
}
{9}

#ACTION {^%1 {flies in|arrives|swims in} {from|from the} %3.$}
{
	#nop {Hook name: arrive};
	#nop {Hook args: name of arrivee.};
	#nop {Hook args: direction from which they arrived.};
	#local {i} {};
	#local {j} {};
	#list {hook[arrive]} {size} {i};
	#if {$i > 0}
	{
		#foreach {*{hook[arrive][]}} {j}
		{
			@${j}{%1;%4}
		}
	}
}
{9}

#ACTION {^%w enters %2.$}
{
	#if {"%1" == "$leader"}
	{
		#regexp {%2} {{.*} {.*}} {enter &2};
		#else
		{
			enter %2
		}
	}
}
{9}

#ACTION {^Alas, you cannot go {down|east|north|south|up|west}...$}
{
	SYNTH_PLAY cannot_go_%1
}
{9}

#ACTION {^The lock springs open.$}
{
	#if {"${auto[track]}" != ""}
	{
		look
	}
}
{9}

#ACTION {^You seem to be unable to pick this lock.$}
{
	#unvariable {auto[track]}
}
{9}

#ALIAS {%i{^news}}
{
	#nop {This is in navigation.tin because it technically overrides a speedwalk.};
	#send {%0}
}
{9}

#EVENT {VARIABLE UPDATE MSDP_AREA_NAME}
{
	#nop {Hook name: areaName};
	#nop {Hook args: the area's name.};
	#local {i} {};
	#local {j} {};
	#list {hook[areaName]} {size} {i};
	#if {$i > 0}
	{
		#foreach {*{hook[areaName][]}} {j}
		{
			@${j}{%1}
		}
	}
}

#EVENT {VARIABLE UPDATE MSDP_ROOM_EXITS}
{
	#local {i} {};
	#local {j} {};
	#if {"%1" != ""}
	{
		#foreach {%1} {i}
		{
			#if {"$i" == "{down|east|north|south|up|west}"}
			{
				#format {j} {%n} {$i}
			};
			#if {"$i" == "C"}
			{
				SYNTH_PLAY door${j}
			}
		}
	}
}

#EVENT {VARIABLE UPDATE MSDP_ROOM_NAME}
{
	#nop {Hook name: roomName};
	#nop {Hook args: the rooms name.};
	#local {i} {};
	#local {j} {};
	#list {hook[roomName]} {size} {i};
	#if {$i > 0}
	{
		#foreach {*{hook[roomName][]}} {j}
		{
			@${j}{%1}
		}
	}
}

#EVENT {VARIABLE UPDATE MSDP_ROOM_VNUM}
{
	#nop {Hook name: roomNumber};
	#nop {Hook args: the rooms vnum.};
	#local {i} {};
	#local {j} {};
	#variable {fleeString} {};
	#switch {%1}
	{
		#case {13200}
		{
			#nop {at Meta Shop, deposit all gold if autodeposit is enabled.};
			#if {${auto[deposit]}}
			{
				#if {$MSDP_GOLD > 0}
				{
					deposit all
				}
			}
		};
		#case {3546}
		{
			#nop {at Midgaard bank, deposit all gold if autodeposit is enabled.};
			#if {${auto[deposit]}}
			{
				#if {$MSDP_GOLD > 0}
				{
					deposit all
				}
			}
		};
		#case {3559}
		{
			#nop {at Adventurers Guild, deposit all gold if autodeposit is enabled.};
			#if {${auto[deposit]}}
			{
				#if {$MSDP_GOLD > 0}
				{
					deposit all
				}
			}
		}
	};
	#list {hook[roomNumber]} {size} {i};
	#if {$i > 0}
	{
		#foreach {*{hook[roomNumber][]}} {j}
		{
			@${j}{%1}
		}
	}
}

#MACRO {\e,}
{
	s
}

#MACRO {\e<}
{
	down
}

#MACRO {\eI}
{
	up
}

#MACRO {\eK}
{
	#ignore {sub} {on};
	scan;
	localecho silent_do #ignore sub off
}

#MACRO {\ei}
{
	n
}

#MACRO {\ej}
{
	w
}

#MACRO {\ek}
{
	scan
}

#MACRO {\el}
{
	e
}

