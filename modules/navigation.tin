#ACTION {%1 {flies|leaves|swims} {down|east|north|south|up|west}.$}
{
	#nop {Hook name: leave};
	#nop {Hook args: flee status 0 for no, 1 for yes.};
	#nop {Hook args: name of leaveee.};
	#nop {Hook args: direction they left.};
	#if {"%1" == "${fleeString}"}
	{
		#variable {fleeString} {1}
	};
	#else
	{
		#variable {fleeString} {0}
	};
	#list {hook[leave]} {size} {i};
	#if {$i > 0}
	{
		#forall {${hook[leave][]}} {@${hook[leave][&0]}{${fleeString};%1;%3}}
	};
	#variable {fleeString} {}
}
{9}

#ACTION {The %1 {above|below|to the .*} seems to be closed.}
{
	#variable {tmp} {%2};
	#replace {tmp} {above} {up};
	#replace {tmp} {below} {down};
	#replace {tmp} {to the } {};
	#regexp {%1} {{.*} {.*}} {open &2 $tmp};
	#else
	{
		open %1 $tmp
	};
	#if {"${auto.track}" != ""}
	{
		look
	}
}
{9}

#ACTION {^%1 {flies in|arrives|swims in} {from|from the} %3.$}
{
	#nop {Hook name: arrive};
	#nop {Hook args: name of arrivee.};
	#nop {Hook args: direction from which they arrived.};
	#list {hook[arrive]} {size} {i};
	#if {$i > 0}
	{
		#forall {${hook[arrive][]}} {@${hook[arrive][&0]}{%1;%4}}
	}
}
{9}

#ACTION {^%w enters %2.$}
{
	#if {"%1" == "$leader"}
	{
		#regexp {%2} {{.*} {.*}} {enter &2};
		#else
		{
			enter %2
		}
	}
}
{9}

#ACTION {^Alas, you cannot go {down|east|north|south|up|west}...$}
{
	SYNTH_PLAY cannot_go_%1
}
{9}

#ACTION {^The lock springs open.$}
{
	#if {"${auto.track}" != ""}
	{
		look
	}
}
{9}

#ACTION {^You seem to be unable to pick this lock.$}
{
	#unvariable {auto.track}
}
{9}

#EVENT {VARIABLE UPDATE MSDP_AREA_NAME}
{
	#nop {Hook name: areaName};
	#nop {Hook args: the area's name.};
	#list {hook[areaName]} {size} {i};
	#if {$i > 0}
	{
		#forall {${hook[areaName][]}} {@${hook[areaName][&0]}{%1}}
	}
}

#EVENT {VARIABLE UPDATE MSDP_ROOM_EXITS}
{
	#if {"%1" != ""}
	{
		#forall {%1}{#if {"&0" == "{down|east|north|south|up|west}"}{#format {i} {%n} {&0}};#if {"&0" == "C"}{SYNTH_PLAY door$i}}
	}
}

#EVENT {VARIABLE UPDATE MSDP_ROOM_NAME}
{
	#nop {Hook name: roomName};
	#nop {Hook args: the rooms name.};
	#list {hook[roomName]} {size} {i};
	#if {$i > 0}
	{
		#forall {${hook[roomName][]}} {@${hook[roomName][&0]}{%1}}
	}
}

#EVENT {VARIABLE UPDATE MSDP_ROOM_VNUM}
{
	#nop {Hook name: roomNumber};
	#nop {Hook args: the rooms vnum.};
	#variable {fleeString} {};
	#switch {%1}
	{
		#case {13200}
		{
			#nop {at Meta Shop, deposit all gold if autodeposit is enabled.};
			#if {${auto[deposit]}}
			{
				#if {$MSDP_GOLD > 0}
				{
					deposit all
				}
			}
		};
		#case {3546}
		{
			#nop {at Midgaard bank, deposit all gold if autodeposit is enabled.};
			#if {${auto[deposit]}}
			{
				#if {$MSDP_GOLD > 0}
				{
					deposit all
				}
			}
		};
		#case {3559}
		{
			#nop {at Adventurers Guild, deposit all gold if autodeposit is enabled.};
			#if {${auto[deposit]}}
			{
				#if {$MSDP_GOLD > 0}
				{
					deposit all
				}
			}
		}
	};
	#list {hook[roomNumber]} {size} {i};
	#if {$i > 0}
	{
		#forall {${hook[roomNumber][]}} {@${hook[roomNumber][&0]}{%1}}
	}
}

#MACRO {\e,}
{
	s
}

#MACRO {\e<}
{
	down
}

#MACRO {\eI}
{
	up
}

#MACRO {\eK}
{
	#ignore {sub} {on};
	scan;
	localecho silent_do #ignore sub off
}

#MACRO {\ei}
{
	n
}

#MACRO {\ej}
{
	w
}

#MACRO {\ek}
{
	scan
}

#MACRO {\el}
{
	e
}
